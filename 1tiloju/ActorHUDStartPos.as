//for use with DefaultActorHUD.as based HUDs

const f32 HUD_X = 350.0f;
const f32 HUD_Y = getScreenHeight();

Vec2f getActorHUDStartPosition(CBlob@ blob, const u8 bar_width_in_slots)
{
    f32 width = bar_width_in_slots * 32.0f;
    return Vec2f( HUD_X + 260 + 50 - width, HUD_Y - 40 );
}

void DrawInventoryOnHUD( CBlob@ this, Vec2f tl )
{
	SColor col;		 
	CInventory@ inv = this.getInventory();	 
	string[] drawn;	
	for (int i = 0; i < inv.getItemsCount(); i++)
	{
		CBlob@ item = inv.getItem(i);
		const string name = item.getName();
		if (drawn.find(name) == -1)
		{
			const int quantity = this.getBlobCount( name );
			drawn.push_back(name);

			GUI::DrawIcon( item.inventoryIconName, item.inventoryIconFrame, item.inventoryFrameDimension, tl+Vec2f(0 + (drawn.length-1) * 32,-6), 1.0f );

			f32 ratio = float(quantity) / float(item.maxQuantity);
			col = ratio > 0.4f ? SColor(255,255,255,255) :
				ratio > 0.2f ? SColor(255,255,255,128) :
				ratio > 0.1f ? SColor(255,255,128,0) : SColor(255,255,0,0);

			GUI::DrawText(""+quantity, tl +Vec2f(8 + (drawn.length-1) * 32 , 24),col );	
		}
	}		

	if (getGameTime() % 60 > 30 && inv.isFull())
	{
		GUI::DrawText("inventory full", tl +Vec2f(22, -20), SColor( 255, 255, 0,0) );	
	}
}

void DrawCoinsOnHUD( CBlob@ this, const int coins, Vec2f tl, const int slot )
{
	if (coins > 0)
	{
		GUI::DrawIconByName( "$COIN$", tl+Vec2f(0 + slot * 32, 0) );
		GUI::DrawText(""+coins, tl +Vec2f(8 + slot * 32 , 24), color_white );
	}
}
